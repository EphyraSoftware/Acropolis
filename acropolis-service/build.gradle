buildscript {
    ext {
        kotlinVersion = "1.2.71"
        springBootVersion = "2.0.4.RELEASE"
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly

    intTestImplementation.extendsFrom testImplementation
}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output

//        java.srcDir file('src/integration-test/java')
    }
}

task('integrationTest', type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    shouldRunAfter test
}

//check.dependsOn integrationTest

//integrationTest.onlyIf { project.hasProperty('runIntegrationTests') }

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
        csv.enabled = false
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.9
            }
        }
    }
}

test {
    useJUnitPlatform()
}

dependencies {
    // The Kotlin standard library
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    // Spring beans dependency so that this library can interact with the Spring IOC container
    compile "org.springframework:spring-context:5.0.8.RELEASE"
    // Needed for spring to work with Kotlin
    compile "org.jetbrains.kotlin:kotlin-reflect"

    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    compile "ch.qos.logback:logback-classic:1.3.0-alpha4"

    compile project(":acropolis-persistence")
    compile project(":acropolis-external")

    // https://mvnrepository.com/artifact/io.kotlintest/kotlintest-runner-junit5
    testCompile "io.kotlintest:kotlintest-runner-junit5:3.1.9"

    // https://mvnrepository.com/artifact/io.mockk/mockk
    testCompile "io.mockk:mockk:1.8.7"
}
